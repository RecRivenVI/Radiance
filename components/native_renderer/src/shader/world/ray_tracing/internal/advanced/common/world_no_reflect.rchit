#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "util/surface_overlay.glsl"
#include "util/disney.glsl"
#include "util/alpha_mode.glsl"
#include "util/random.glsl"
#include "util/ray_cone.glsl"
#include "util/ray.glsl"
#include "util/sampling_helpers.glsl"
#include "util/util.glsl"
#include "common/shared.hpp"
#include "common/chunk_lookup.glsl"

layout(set = 0, binding = 0) uniform sampler2D textures[];

#include "util/emissive_overlay.glsl"

layout(set = 1, binding = 0) uniform accelerationStructureEXT topLevelAS;

layout(set = 1, binding = 1) readonly buffer BLASOffsets {
    uint offsets[];
}
blasOffsets;

layout(set = 1, binding = 2) readonly buffer IndexBufferAddr {
    uint64_t addrs[];
}
indexBufferAddrs;

layout(set = 1, binding = 3) readonly buffer LastIndexBufferAddr {
    uint64_t addrs[];
}
lastIndexBufferAddrs;

layout(set = 1, binding = 8) readonly buffer LastObjToWorldMat {
    mat4 mat[];
}
lastObjToWorldMats;

layout(set = 1, binding = 7) readonly buffer TextureMappingBuffer {
    TextureMapping mapping;
};

layout(set = 2, binding = 0) uniform WorldUniform {
    WorldUBO worldUBO;
};

layout(set = 2, binding = 1) uniform LastWorldUniform {
    WorldUBO lastWorldUbo;
};

layout(set = 2, binding = 2) uniform SkyUniform {
    SkyUBO skyUBO;
};

layout(set = 3, binding = 1, rgba8) uniform image2D diffuseAlbedoImage;
layout(set = 3, binding = 2, rgba8) uniform image2D specularAlbedoImage;
layout(set = 3, binding = 3, rgba16f) uniform image2D normalRoughnessImage;
layout(set = 3, binding = 4, rg16f) uniform image2D motionVectorImage;
layout(set = 3, binding = 5, r16f) uniform image2D linearDepthImage;

layout(std430, buffer_reference, buffer_reference_align = 8) readonly buffer IndexBuffer {
    uint indices[];
}
indexBuffer;

#include "util/vertex.glsl"
#include "util/geometry_backface_debug.glsl"
#include "util/glint_material.glsl"

layout(location = 0) rayPayloadInEXT MainRay mainRay;
hitAttributeEXT vec2 attribs;

void main() {
    vec3 viewDir = -mainRay.direction;

    uint instanceID = gl_InstanceCustomIndexEXT;
    uint geometryID = gl_GeometryIndexEXT;

    uint geometryBufferIndex = getGeometryBufferIndex(instanceID, geometryID);
    if (geometryBackfaceDebug(mainRay, instanceAppearances.values[geometryBufferIndex].materialFlags)) return;

    uint i0;
    uint i1;
    uint i2;
    PositionVertex p0;
    PositionVertex p1;
    PositionVertex p2;
    MaterialVertex m0;
    MaterialVertex m1;
    MaterialVertex m2;
    loadTriangle(geometryBufferIndex, gl_PrimitiveID, i0, i1, i2, p0, p1, p2, m0, m1, m2);

    vec3 baryCoords = vec3(1.0 - (attribs.x + attribs.y), attribs.x, attribs.y);
    vec3 worldPos = gl_WorldRayOriginEXT + gl_WorldRayDirectionEXT * gl_HitTEXT;
    uint coordinate = getCoordinate(m0.packedData);
    vec3 normal = baryCoords.x * m0.norm + baryCoords.y * m1.norm + baryCoords.z * m2.norm;
    if (coordinate == 1) {
        normal = normalize(mat3(worldUBO.cameraViewMatInv) * normal);
    } else {
        normal = normalize(normal);
    }

    bool useColorLayer = hasColorLayer(m0.packedData);
    bool colorLayerMix = hasColorLayerMix(m0.packedData);
    vec4 colorLayerValue;
    if (useColorLayer) {
        colorLayerValue = baryCoords.x * m0.colorLayer + baryCoords.y * m1.colorLayer + baryCoords.z * m2.colorLayer;
    } else {
        colorLayerValue = vec4(1.0);
    }
    vec3 colorLayer = colorLayerValue.rgb;

    bool useTexture = hasTexture(m0.packedData);
    float albedoEmission =
        baryCoords.x * m0.albedoEmission + baryCoords.y * m1.albedoEmission + baryCoords.z * m2.albedoEmission;
    uint textureID = m0.textureID;
    uint alphaMode = getAlphaMode(m0.packedData);
    vec4 albedoValue;
    vec4 specularValue;
    vec4 normalValue;
    vec2 textureUV = vec2(0.0);
    float lod = 0.0;
    if (useTexture) {
        int specularTextureID = mapping.entries[textureID].specular;
        int normalTextureID = mapping.entries[textureID].normal;
        textureUV = baryCoords.x * m0.textureUV + baryCoords.y * m1.textureUV + baryCoords.z * m2.textureUV;
        vec2 atlasUvMin = min(m0.textureUV, min(m1.textureUV, m2.textureUV));
        vec2 atlasUvMax = max(m0.textureUV, max(m1.textureUV, m2.textureUV));

        // ray cone
        float coneRadiusWorld = mainRay.coneWidth + gl_HitTEXT * mainRay.coneSpread;
        vec3 dposdu, dposdv;
        computedposduDv(p0.pos, p1.pos, p2.pos, m0.textureUV, m1.textureUV, m2.textureUV, dposdu, dposdv);
        lod = lodWithObjectCone(textures[nonuniformEXT(textureID)], coneRadiusWorld, mat3(gl_ObjectToWorldEXT),
            p0.pos, p1.pos, p2.pos, m0.textureUV, m1.textureUV, m2.textureUV);
        albedoValue = sampleTexture(textures[nonuniformEXT(textureID)], textureUV, lod, false);
        float surfaceAlpha = colorLayerMix ? albedoValue.a : albedoValue.a * colorLayerValue.a;
        albedoValue.a = resolveSurfaceAlpha(surfaceAlpha, alphaMode);
        if (isCoverageAlphaMode(alphaMode) || isAdditiveAlphaMode(alphaMode)) {
            albedoValue.a = 1.0;
        }
        if (specularTextureID >= 0) {
            specularValue = sampleTexture(textures[nonuniformEXT(specularTextureID)], textureUV, lod, false);
        } else {
            specularValue = vec4(0.0);
        }
        if (normalTextureID >= 0) {
            normalValue = samplePBRTexture(textures[nonuniformEXT(normalTextureID)], textureUV, atlasUvMin, atlasUvMax,
                                           lod, ADV_PBR_SAMPLING_MODE);
        } else {
            normalValue = vec4(0.0);
        }
    } else {
        albedoValue = vec4(1.0);
        specularValue = vec4(0.0);
        normalValue = vec4(0.0);
    }

    bool useGlint = hasGlint(m0.packedData);
    uint glintTexture = m0.glintTexture;
    vec2 glintUV = baryCoords.x * m0.glintUV + baryCoords.y * m1.glintUV + baryCoords.z * m2.glintUV;
    glintUV = transformGlintUv(worldUBO.textureMat, glintUV, m0.packedData);
    vec3 glint = useGlint ? sampleTexture(textures[nonuniformEXT(glintTexture)], glintUV, false).rgb : vec3(0.0);
    glint = glint * glint;

    bool useOverlay = hasOverlay(m0.packedData);
    vec3 baseTint = colorLayerMix ?
                        applySurfaceOverlay(albedoValue.rgb, colorLayer, colorLayerValue.a) :
                        albedoValue.rgb * colorLayer;
    vec3 tint = baseTint;
    if (useOverlay) {
        ivec2 overlayUV = m0.overlayUV;
        vec4 overlayColor = sampleTexture(textures[nonuniformEXT(worldUBO.overlayTextureID)], overlayUV, 0, false);
        tint = applySurfaceOverlay(baseTint, overlayColor.rgb, 1.0 - overlayColor.a);
    }

    albedoValue = vec4(tint, albedoValue.a);
    LabPBRMat mat = convertLabPBRMaterial(albedoValue, specularValue, normalValue,
                                          isTransmissionAlphaMode(alphaMode));
    vec3 glintRadiance = applyGlintMaterialLayer(mat, glint);

    // add glowing radiance
    mainRay.radiance += 12 * tint * mat.emission * mainRay.throughput;
    mainRay.radiance += tint * albedoEmission * mainRay.throughput;
    mainRay.radiance += sampleEmissiveOverlay(m0.emissiveOverlayTextureID, textureUV, lod) * mainRay.throughput;
    mainRay.radiance += glintRadiance * mainRay.throughput;
    mainRay.hitT = gl_HitTEXT;
    mainRay.normal = vec3(0.0);
    rayStoreMaterial(mainRay, albedoValue, mat.f0, mat.roughness, mat.metallic, mat.transmission, mat.ior, mat.emission);
    raySetNoisy(mainRay, false);
    mainRay.hasPrevScenePos = 0u;
    raySetStop(mainRay, true);
}
